grammar org.eclipse.emf.mwe.properties.MWEProperties 

generate mweProperties "http://www.eclipse.org/emf/mwe/properties/MWEProperties"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

PropertyFile :
  (entries+=NaturalLine)*;
	
NaturalLine:
    BlankLine 
  | PropertyComment 
  | PropertyEntry;
 
BlankLine:
  {BlankLine} WS? (LINE_TERMINATOR);
  
PropertyComment:
  WS? value=SL_COMMENT (LINE_TERMINATOR);
  
PropertyEntry:
  WS? name=Key 
  (   delimiter=Delimiter value=CompoundStringLeadingDelimiter
    | delimiter=SimpleDelimiter value=CompoundString
  );
   
Delimiter:
    WS? ('='|':') WS?
;

SimpleDelimiter:
    WS
;

CompoundString:
    {CompoundString} (LINE_TERMINATOR)
  | parts+=CompoundStringPart
    ('\\' LINE_TERMINATOR WS? parts+=CompoundStringPartLeadingDelimiter)*
    (LINE_TERMINATOR)
;

CompoundStringLeadingDelimiter returns CompoundString:
    {EmptyPropertyValue} (LINE_TERMINATOR)
  | parts+=CompoundStringPartLeadingDelimiter
    ('\\' LINE_TERMINATOR WS? parts+=CompoundStringPartLeadingDelimiter)*
    (LINE_TERMINATOR);
 
CompoundStringPartLeadingDelimiter returns CompoundStringPart:
  value=CompoundStringPartLeadingDelimiterValue
;

CompoundStringPartLeadingDelimiterValue:
  NonWSConstant (WS NonWSConstant?)*
;

CompoundStringPart:
  value=CompoundStringPartValue
;

CompoundStringPartValue:
  Key (WS NonWSConstant?)*
;
	
NonWSConstant:
  NonWsNonCommentConstant | SL_COMMENT;
 
NonWsNonCommentConstant:
    Key ((':' | '=') Key?)*;
 
Key:
  (FQN | '\\:' | '\\=' | '/')+ ;
  
FQN:
  ID ('.' ID)*;
  
terminal LINE_TERMINATOR: '\r' '\n'? | '\n' ;
  
terminal ID  		: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
 
terminal SL_COMMENT : ('#'|'!') !('\n'|'\r')*;

//terminal EOF        :  "synthetic:EOF";

terminal WS			: (' '|'\t')+;

terminal ANY_OTHER  : .;